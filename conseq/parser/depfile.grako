@@eol_comments :: /#.*?$/

triple_dbl_quoted_string = /"""(?:[^"]|"{1,2}(?!"))+"""/ ;
dbl_quoted_string = /"[^"]*"/ ;
triple_squoted_string = /'''(?:[^']|'{1,2}(?!'))+'''/ ;
squoted_string = /'[^']*'/ ;
quoted_string = triple_dbl_quoted_string | dbl_quoted_string | squoted_string | triple_squoted_string ;

identifier = /[A-Za-z]+[A-Za-z0-9_+-]*/ ;
url = /\S+/ ;

json_value = quoted_string | json_obj ;
json_name_value_pair = quoted_string ":" json_value ;
json_obj = "{" json_name_value_pair { "," json_name_value_pair }* "}" ;

query_variable = identifier ;
query_name_value_pair = quoted_string ":" ( json_value | query_variable ) |
    quoted_string "~" quoted_string ;
query_obj = "{" query_name_value_pair { "," query_name_value_pair }* "}" ;

input_spec_each = identifier "=" query_obj ;
input_spec_all = identifier "=" "all" query_obj ;
input_spec = input_spec_each | input_spec_all ;
input_specs = input_spec { "," input_spec }* ;

output_specs = json_obj { "," json_obj } ;

type_def = "type" identifier "has" "{" identifier { "," identifier }* "}" ;

expected_output_type = identifier { "?" | "+" | {} } ;
expected_output_types = expected_output_type { "," expected_output_type }* ;

run_statement = "run" quoted_string [ "with" quoted_string ] ;

requirement_def = identifier "=" /[0-9]+/ ;

rule_parameters = ("inputs" ":" input_specs |
  "outputs" ":" output_specs |
  "expect-outputs" ":" expected_output_types |
  "options" ":" identifier { "," identifier }* |
  "executor" ":" identifier |
  "requires" ":" requirement_def { "," requirement_def }* )
  ;

rule = "rule" identifier ":" { rule_parameters } * { run_statement } * 
;

xref = "xref" url json_obj ;

add_if_missing = "add-if-missing" json_obj ;

exec_profile = "exec-profile" identifier json_obj;

var_stmt = "let" identifier "=" quoted_string ;

include_stmt = "include" quoted_string ;

declarations =  { rule | xref | include_stmt | var_stmt | add_if_missing | type_def | exec_profile }+  $ ;
