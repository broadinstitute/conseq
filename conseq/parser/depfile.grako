@@grammar :: depfile
@@eol_comments :: /#.*?$/

triple_dbl_quoted_string = /"""(?:[^"]|"{1,2}(?!"))+"""/ ;
dbl_quoted_string = /"[^"]*"/ ;
triple_squoted_string = /'''(?:[^']|'{1,2}(?!'))+'''/ ;
squoted_string = /'[^']*'/ ;
quoted_string = triple_dbl_quoted_string | dbl_quoted_string | squoted_string | triple_squoted_string ;

identifier = /[A-Za-z]+[A-Za-z0-9_+-]*/ ;

json_value = quoted_string | json_obj | json_array ;
json_array = "[" json_value {"," json_value} * "]" | "[" "]" ;
json_name_value_pair = quoted_string ":" json_value ;
json_obj = "{" json_name_value_pair { "," json_name_value_pair }* "}" ;

query_variable = identifier ;
query_name_value_pair = quoted_string ":" ( json_value | query_variable ) |
    quoted_string "~" quoted_string ;
query_obj = "{" query_name_value_pair { "," query_name_value_pair }* "}" ;

input_spec_each = identifier "=" query_obj ;
input_spec_all = identifier "=" "all" query_obj ;
input_spec = input_spec_each | input_spec_all ;
input_specs = input_spec { "," input_spec }* ;

output_specs = json_obj { "," json_obj } ;

run_statement = "run" quoted_string [ "with" quoted_string ] ;

requirement_def = identifier "=" /[0-9]+/ ;

output_expected_key_value = quoted_string { { ":" quoted_string } | {} } ;
output_expected_def = "{" output_expected_key_value { "," output_expected_key_value }* "}";
outputs_expected_defs = output_expected_def { "," output_expected_def }* ;

rule_parameters = ("inputs" ":" input_specs |
  "if-defined" ":" identifier { "," identifier }* |
  "outputs" ":" output_specs |
  "outputs-expected" ":" outputs_expected_defs |
  "options" ":" identifier { "," identifier }* |
  "executor" ":" identifier |
  "publish" ":" quoted_string |
  "resources" ":" json_obj |
  "requires" ":" requirement_def { "," requirement_def }* )
  ;

rule = "rule" identifier ":" { rule_parameters } * { run_statement } *
;

add_if_missing = "add-if-missing" json_obj ;

remember_executed_input = "input" quoted_string ":" {json_obj | json_array};
remember_executed_output = "output" ":" json_obj;
remember_executed = "remember-executed" "transform" ":" quoted_string { remember_executed_input } * { remember_executed_output } * ;

exec_profile = "exec-profile" identifier json_obj;

var_stmt = "let" identifier "=" quoted_string ;

include_stmt = "include" quoted_string ;

declarations =  { rule | include_stmt | var_stmt | add_if_missing | exec_profile | remember_executed }+  $ ;

